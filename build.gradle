plugins {
	id 'maven-publish'
	alias libs.plugins.fabric.loom
}

base.archivesName = project.archives_base_name
version = "$project.version+${libs.versions.minecraft.get()}"
group = project.maven_group

loom {
	accessWidenerPath = file("src/main/resources/corners.accesswidener")
}

repositories {
	mavenLocal()
	maven {
		name = "ParchmentMC"
		url = "https://maven.parchmentmc.org/"
	}
//	maven {
//		url = 'https://ladysnake.jfrog.io/artifactory/mods'
//    }
	maven {
		url = 'https://jitpack.io'
    }
	maven {
		url = 'https://maven.shedaniel.me/'
	}
	maven {
		url = 'https://maven.terraformersmc.com/'
	}
	maven {
		url 'https://api.modrinth.com/maven'
		content {
			includeGroup "maven.modrinth"
		}
    }
}

dependencies {
	minecraft libs.minecraft
	mappings loom.layered {
		officialMojangMappings()
		parchment("org.parchmentmc.data:parchment-1.20.1:2023.09.03@zip")
	}
	modImplementation libs.fabric.loader
	modImplementation libs.fabric.api

	modImplementation libs.terraformersmc.modmenu

	includeMod libs.shedaniel.cloth

	includeMod libs.ludocrypt.specialmodels
	includeMod libs.ludocrypt.limlib

}

def includeMod(var dep) {
	dependencies.modImplementation(dep)
	dependencies.include(dep)
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}
java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
	withSourcesJar()
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	repositories {

	}
}
